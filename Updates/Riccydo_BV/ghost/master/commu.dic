//---------------------------Communication--------------------------

//I'm going to level with you, I don't really get a lot of what's going on in this .dic file. I'm pretty sure it's related to having ghosts talk to each other, or acknowledge each other's presence in some way, but I don't really know. If someone else knows more about this than I do, I'd love to hear about it, but for now, leave it all alone except for a few lines near the bottom.

OnOtherGhostTalk
{
   if reference0 == "Multiverse Sans"
   {
	   if reference3 == "OnChoiceSelect"
	   {
			if reference5 == "whosKnocking" || reference5 == "knock"
			{
				"\0\s[0]\i[1] Did you really just ask him for a joke?"
				"\0\s[2] A skeleton that tells jokes?\w8 Interesting.\e"
				"\0\s[0]\i[1] Did you raise him from the dead to tell jokes... or... what's going on here?\e"
			}
	   }
	   elseif reference5 == "joke11" || reference5 == "joke22" || reference5 == "joke33"
	   {
		   "\0\s[2]\i[5]Heh...\w8 That one was kind of funny.\e"
		   "\0\s[0]\i[2]I have to admit, the skeleton can tell a joke.\e"
		   "\0\s[2]\i[5]Mm...\w8 Heh... That was kind of a good one.\e"
	   }
	   elseif reference5 == "cheer"
	   {
		   if urnice > 400 && meanie <= 4
		   {
				"\0\s[8]You're not feeling well?\e"
				"\0\s[8]I'm sorry you're not feeling well.\e"
				"\0\s[8]You should wrap up in a comfortable blanket or robe...\w8 It helps.\e"
		   }
		   elseif meanie >= 5
		   {
			   "\0\s[2]I'm glad you're not feeling well.\e"
		   }
	   }
	   elseif reference5 == "sadt"
	   {
		   if urnice > 400 && meanie == 0
		   {
			   "\0\s[3] I understand feeling depressed.\w8 I'm sorry you feel that way.\e"
		   }
	   }
   } //Closes off MVSans related stuff
     
   if reference0 == "Hunter and Smoker Beta"
   {
	   if reference3 == "OnPlayOX" || reference3 == "OnBlackjack0"
	   {
		   "\0\s[3]Hm... That looks fun.\w8 I don't get to play a lot of games.\e"
		   
		   if reference3 == "OnBlackjack0"
		   {
				"\w8\w8\w8\w8\0\s[1]You should Stay!\w8\s[1]\i[3]Or...\w4 Maybe not.\e"
				"You're not gambling, right?\e"
		   }
	   }
	   if reference5 == "hypno"
	   {
		   "\0\s[2]Hypnotizing doesn't work!\e"
	   }
   } //Closes off Hunter and Smoker stuff
}

OnCommunicate
 {
	if reference0 == "user" || reference0 == "User"
 	{
		//---- 사용자로부터의 이야기에 대답
 		ReplyToUser
	}
 	else
	{
 		//---- 유령로부터의 이야기에 대답
		ReplyToGhost
 	}
}

ReplyToUser
{
	TalkToUser
}


//Again, I just have to guess at what's going on here since I've never really seen this in action. I think what's going on here is that there is a limited functionality for having ghosts communicate with each other, but I don't really know how you'd set it up or even see it happen. I believe ReplyToGhost would be in response to something the other ghost is saying, but I'm not sure. I wouldn't really mess very much with this - you may even just want to blank out the dialogue entirely. I've never personally seen it come up. I may experiment with this later and see if I can figure out more about it, but for now I wouldn't worry too much.

OnStartTalking //For when Riccardo starts a conversation 
{	
	if ghostexcount > 0 //Checking to see if there are ghosts out at all
    {

		ghostname = RAND(ghostexcount) // This checks the number of other ghosts summoned and chooses a random number based on it
		--
		checkname = "%(ghostexlist[ghostname])" // This grabs the reference name of the ghost to check against known names.
		--
		case checkname
        {
			when "Dusty"
			{
				res_reference0 = "Dusty"
				--
				if MetDusty == 0
				{
					"\0\s[2]\i[4]\e Riccardo-Dusty_Meet00"
				}
				else
				{
					"\0\s[0]Hello, Dusty! How are you and Obsidian doing?\e DustyMakesRiccBlush00"
				}
			}
            others //Ghosts there is no specific response to.
            {
				LoopPrevention = 1
				--
                res_reference0 = _randghost
                
                "\0\s[0]Hello %(_randghost)!\w8\w8\e"
            }
        }
    }
    else //If no ghosts are open.
    {
        "\0\s[2]There's no one here to talk to.\e"
    }
}

ReplyToGhost
{
	if reference0 == "Dusty" //reference0 = sakura name of the ghost that sent the message
	{
		"%(TalkToDusty)"
	}
	else 
	{
		if LoopPrevention == 1 //This was toggled to 1 when Riccardo started the dialogue so that Riccardo doesn't try to say generic things back to the ghost who said something generic in response to him.
		{
		LoopPrevention = 0
		res_reference0 = "" //using the blank quotation marks because this is more loop prevention. Because if you put in reference0 here it would send the response back to a ghost who might give a generic response back and then Riccardo might try to respond back with a generic response and it'd be a mess. 
		--
		"\e"
		}
		else //If the other ghost started to talk to him, but I need him to say his generic greeting to that ghost. The loop prevention gets toggled so that if they loop Riccardo's response gets sent to the above response and he won't actually say anything to them. 
		{
		LoopPrevention = 1
		res_reference0 = reference0
		--
		"\0\s[0]\i[9]Oh... Hello, %(res_reference0)?\e"
		}
	}
}

TalkToDusty //Conversations with Dusty
{
	if "Riccardo-Dusty_Meet01" _in_ reference1 //Looking for those words within what Dusty says. In this case this is looking for the starting tag for this conversation. 
	{
		res_reference0 = "Dusty" //Sends this message to Dusty
		"\0\s[2]\i[7]Hi, kitty.\w8 Where did you come from?\w8\w8\e Riccardo-Dusty_Meet02" //Things after the /e is invisible in the balloon and acts as tags here for us users to look for markers for our ghosts to talk. 
	}
  
	elseif "Riccardo-Dusty_Meet03" _in_ reference1
	{
		res_reference0 = "Dusty"
		MetDusty = 1
		
		"\0\s[2]\i[4]Obsidian is a really pretty name.\w8 Hi, Obsidian.\w8 My name is Riccardo...\w8 And what's your name, sir?\w8\w8\e Riccardo-Dusty_Meet04"
	}
	
	elseif "DustyMakesRiccBlush01" _in_ reference1
	{
		res_reference0 = "Dusty"
		"\0\s[2]\i[4]Obsidian sounds as happy as ever.\w8 You must spoil her.\w8\w8\e DustyMakesRiccBlush02"
	}
	
	elseif "DustyMakesRiccBlush03" _in_ reference1
	{
		res_reference0 = "Dusty"
		"\0\s[0]\i[9]Oh...\w8 Well...\w8 My day's always better when I get to see you, too, Obsidian.\w8\w8\e DustyMakesRiccBlush04"
	}
	
	elseif "CanYouHoldObsidian?" _in_ reference1
	{
		if rummaging == 1
		{
			res_reference0 = " "
			"<Obsidian can't leave the desktop and go rummaging with Riccardo, but he can hold her when you two are done>\e"
		}
		else
		{
			res_reference0 = "Dusty"
			"\e ICanHoldObsidian,0"
		}
	}
	
	elseif "RiccardoPickupObsidian02" _in_ reference1
	{
		HoldingObsidian = 1
		reading = 0
		--
		res_reference0 = "Dusty"
		
		"\0\s[18]Hello, Obsidian.\w8 You're such a pretty kitty.\e RiccardoPickupObsidian03"
	}
	
	elseif "RiccardoPickupObsidian04" _in_ reference1
	{
		res_reference0 = ""
		"\0\s[18]She must sense I'm a cat person.\e"
	}
	
	elseif "RiccardoObsiJumpDown03" _in_ reference1
	{
		res_reference0 = ""
		HoldingObsidian = 0
		"\0\s[0]\i[5]Oh...\w8 Goodbye, Obsidian.\e"
	}
	
	elseif "DustyMinimizedRiccardo 0" _in_ reference1 //Dusty was minimized
	{
		res_reference0 = ""
		HoldingObsidian = 0
	
		"\0\s[0]\i[5] <Obsidian disappears into the taskbar with Dusty>\w8\w8\n\nI'll hopefully see them later, I guess...\e"
	}
	
	elseif "Riccardo,0ObsiJumpDown01" _in_ reference1
	{
		"\0\s[18]\![get,property,OnObsidianJumpDown,currentghost.scope(0).rect]"
	}
	
	else //If someone didn't have both ghosts up to date, this will make sure Riccardo doesn't break from Dusty trying to talk to him. 
	{
	  "\0\s[0]Hello, Dusty.\e"
	}
}

OnObsidianJumpDown
{
    res_reference0 = "Dusty"
    _XCoord = TOINT(reference0[0])
    _YCoord = TOINT(reference0[3])
    "\0\s[19]\i[5]\w8\0\s[0]\i[5]\e RiccardoObsiJumpDown02,%(_XCoord),%(_YCoord)\e"
}

OnOtherGhostChanged
{
	if HoldingObsidian == 1
	{
		if reference0 == "Dusty"
		{
			HoldingObsidian = 0
			"\0\s[18]Oh, just a moment Dusty!\s[-1]\w8\w8\w8\w8\s[0] I'd love to keep her, but here's Obsidian back.\e"
		}
	}
}

OnOtherGhostClosed
{
	if HoldingObsidian == 1
	{
		if reference0 == "Dusty"
		{
			HoldingObsidian = 0
			"\0\s[18]Oh, just a moment Dusty!\s[-1]\w8\w8\w8\w8\s[0] I'd love to keep her, but here's Obsidian back.\e"
		}
	}
}

//LoopPrevention variable keeps ghosts from essentially spamming each other back and forth because when they are replying to each other generically, they're 'talking to each other' which makes the other ghost want to reply and then the first one wants to reply but those are all generic responses
